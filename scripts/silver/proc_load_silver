/*
===============================================================================
Stored Procedure: Load Silver Layer (Bronze -> Silver)
===============================================================================
Script Purpose:
    This stored procedure performs the ETL (Extract, Transform, Load) process to 
    populate the 'silver' schema tables from the 'bronze' schema.
	Actions Performed:
		- Truncates Silver tables.
		- Inserts transformed and cleansed data from Bronze into Silver tables.
		
Parameters:
    None. 
	  This stored procedure does not accept any parameters or return any values.

Usage Example:
    EXEC Silver.load_silver;
===============================================================================
*/

Create or Alter Procedure silver.load_silver as 
BEGIN
		Declare @start_time DATETIME, @end_time DATETIME, @batch_start_time DATETIME, @batch_end_time DATETIME;
	BEGIN TRY
		SET @batch_start_time = GETDATE()
		Print '=====================================================';
		Print 'Loading Silver Layer';
		Print '=====================================================';

		Print '-----------------------------------------------------';
		Print 'Loading CRM Tables';
		Print '-----------------------------------------------------';

		Set @start_time = GETDATE();
		PRINT '>> Truncating Table: [silver].[crm_cust_info]';
		Truncate Table [silver].[crm_cust_info];
		Print '>> Inserting Data Into: [silver].[crm_cust_info]';
		INSERT INTO [silver].[crm_cust_info] (
			   [cst_id]
			  ,[cst_key]
			  ,[cst_firstname]
			  ,[cst_lastname]
			  ,[cst_marital_status]
			  ,[cst_gndr]
			  ,[cst_create_date]
		)
		select 
				cst_id,
				cst_key,
				TRIM(cst_firstname) AS cst_firstname,
				TRIM(cst_lastname) AS cst_lastname,
				CASE WHEN upper(cst_marital_status) = 'S' THEN 'Single'
					 WHEN upper(cst_marital_status) = 'M' THEN 'Married'
					 else 'n/a'
				END cst_marital_status,
				CASE WHEN upper(cst_gndr) = 'F' THEN 'Female'
					 WHEN upper(cst_gndr) = 'M' THEN 'Male'
					 else 'n/a'
				END cst_gndr,
				cst_create_date
		from (
			select 
			*,
			ROW_NUMBER() over (partition by cst_id order by cst_create_date desc) as Flag_last
			from bronze.crm_cust_info
			Where cst_id is not null) t
		where Flag_last =1 
		Set @end_time = GETDATE()
		Print '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + 'Seconds';

		Print '-----------------------------------------------------';
		SET @start_time = GETDATE();
		Print '>> Truncating Table: [silver].[crm_prd_info]';
		Truncate Table [silver].[crm_prd_info];
		Print '>> Inserting Data Into: [silver].[crm_cust_info]';
		insert into [silver].[crm_prd_info] (
  			   [prd_id]
			  ,[prd_key]
			  ,[cat_id]
			  ,[prd_nm]
			  ,[prd_cost]
			  ,[prd_line]
			  ,[prd_start_dt]
			  ,[prd_end_dt]
		)
		SELECT [prd_id]
			  ,SUBSTRING(prd_key, 7, len(prd_key)) as prd_key
			  ,Replace(SUBSTRING(prd_key, 1, 5),'-','_') as cat_id
			  ,[prd_nm]
			  ,ISNULL([prd_cost] , 0 ) AS prd_cost
			  ,CASE WHEN UPPER(TRIM(prd_line)) = 'M' THEN 'Mountain'
					WHEN UPPER(TRIM(prd_line)) = 'R' THEN 'Road'
					WHEN UPPER(TRIM(prd_line)) = 'S' THEN 'Other Sales'
					WHEN UPPER(TRIM(prd_line)) = 'T' THEN 'Touring'
					ELSE 'n/a'
			   END AS [prd_line]
			  ,cast([prd_start_dt] AS date) as prd_start_dt
			  ,CAST(LEAD(prd_start_dt) over(partition by prd_key order by prd_start_dt)-1 as date) AS prd_end_dt
		  FROM [DataWareHouse].[bronze].[crm_prd_info]
		  Set @end_time = GETDATE()
		  Print '>> Load Duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + 'Secondes';
	   	  Print '-----------------------------------------------------';

        SET @start_time = GETDATE();
		Print'>> Truncating Table: [silver].[crm_sales_details]';
		Truncate Table [silver].[crm_sales_details];
		Print '>> Inserting Data Into: [silver].[crm_cust_info]';
		  INSERT INTO [silver].[crm_sales_details] (
				sls_ord_num,
				sls_prd_key,
				sls_cust_id,
				sls_order_dt,	
				sls_ship_dt,
				sls_due_dt,
				sls_sales,
				sls_quantity ,
				sls_price
		)
		SELECT [sls_ord_num]
			  ,[sls_prd_key]
			  ,[sls_cust_id]
			  ,CASE WHEN [sls_order_dt] = 0 OR LEN([sls_order_dt]) !=8 Then Null
					ELSE CAST(Cast([sls_order_dt] AS varchar) AS date) 
			   END [sls_order_dt]
			  ,CASE WHEN [sls_ship_dt] = 0 OR LEN([sls_ship_dt]) !=8 Then Null
					ELSE CAST(Cast([sls_ship_dt] AS varchar) AS date) 
			   END [sls_ship_dt]
			  ,CASE WHEN [sls_due_dt] = 0 OR LEN([sls_due_dt]) !=8 Then Null
					ELSE CAST(Cast([sls_due_dt] AS varchar) AS date) 
			   END [sls_due_dt]
			  ,CASE WHEN sls_sales IS NULL OR sls_sales <=0  OR sls_sales != sls_quantity * ABS(sls_price)
					 THEN sls_quantity * ABS(sls_price)
					 ELSE sls_sales
				END sls_sales
			  ,[sls_quantity]
			  ,CASE WHEN sls_price IS NULL OR sls_price <=0
					 THEN sls_sales / NULLIF(sls_quantity, 0)
					 ELSE sls_price
				END sls_price
		  FROM [DataWareHouse].[bronze].[crm_sales_details]
		  SET @end_time = GETDATE()
		  Print 'Load duration: ' + CAST (DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + 'Seconds' ;
		Print '----------------------------------------------------------------------------';

		SET @start_time = GETDATE();
		Print'>> Truncating Table: silver.erp_CUST_AZ12';
		Truncate Table silver.erp_CUST_AZ12;
		Print '>> Inserting Data Into: [silver].[crm_cust_info]';
		insert into  silver.erp_CUST_AZ12 (CID, BDATE, GEN)
		select
			   case when cid like 'NAS%' Then SUBSTRING(cid, 4 , LEN(cid)) 
					Else CID
			   End cid,
			   CASE When BDATE > GETDATE() Then NULL
					else BDATE
				end bdate,
				CASE When upper(TRIM(GEN)) in ( 'FEMALE','F') then 'Female'
							 When upper(TRIM(GEN)) in ( 'MALE','M') then 'Male'
							 Else 'n/a'
						END Gen
		from bronze.erp_CUST_AZ12

		SET @end_time = GETDATE();
		Print 'Load duration: ' + CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + 'seconds';
		Print '---------------------------------------------------------------------';

		Set @start_time = GETDATE();
		Print '>> Truncating Table: silver.erp_LOC_A101';
		Truncate Table silver.erp_LOC_A101
		Print '>> Inserting Data Into: [silver].[crm_cust_info]';
		INSERT INTO silver.erp_LOC_A101 (CID, CNTRY)
		select 
			   REPLACE(cid, '-', '') as 'CID',
			   CASE WHEN TRIM(CNTRY)= 'DE' Then 'Germany'
					 WHEN TRIM(CNTRY) IN('US', 'USA') Then 'United States'
					 WHEN TRIM(CNTRY) = '' OR CNTRY IS Null Then 'n/a'
					 else TRIM(CNTRY)
				END CNTRY
		from [bronze].[erp_LOC_A101]
		Set @end_time = GETDATE();
		Print 'Load duration: '+ CAST(DATEDIFF(second, @start_time, @end_time) AS NVARCHAR) + 'Seconds';
		Print '---------------------------------------------------------------------';

		Set @start_time = GETDATE();
		Print '>> Truncate Table: silver.erp_PX_CAT_G1V2';
		Truncate Table silver.erp_PX_CAT_G1V2;
		Print '>> Inserting Data Into: [silver].[crm_cust_info]';
		INSERT INTO silver.erp_PX_CAT_G1V2 (ID, CAT, SUBCAT, MAINTENANCE)
		select   ID,
				 CAT,
				 SUBCAT,
				 MAINTENANCE
		from bronze.erp_PX_CAT_G1V2
		Set @end_time = GETDATE();
		Print 'Load Duration: ' + CAST( DATEDIFF(second, @start_time, @end_time) AS NVARCHAR)  + 'Seconds';
		SET @batch_end_time = GETDATE();
		print '============================================================================';
		Print 'Loading silver layer is completed';
		Print '>>>> TOTAL LOAD DURATION: ' + CAST(DATEDIFF( second, @batch_start_time, @batch_end_time) AS NVARCHAR) + 'Seconds';
		print '============================================================================';
	END TRY
	BEGIN CATCH
		Print '==================================================================';
		Print 'EROR OCCURED DURING LOADING SILVER LAYER '
		Print '==================================================================';
		Print 'ERROR MESSAGE ' + ERROR_MESSAGE();
		Print 'ERROR MESSAGE' + CAST(ERROR_NUMBER() AS NVARCHAR);
		Print 'ERROR STATE' + CAST(ERROR_STATE()AS NVARCHAR);
		Print '==================================================================';
	END CATCH
END

